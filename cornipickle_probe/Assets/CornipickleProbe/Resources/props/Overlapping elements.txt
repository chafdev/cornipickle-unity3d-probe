# We define what it means x intersection
We say that $x xintersects $y when (
  ($x's left is less than ($y's left + $y's width))
   And
  ($y's left is less than ($x's left + $x's width))
).

# We define what it means y intersection
We say that $x yintersects $y when (
  ($x's top is less than ($y's top + $y's height))
   And
  ($y's top is less than ($x's top + $x's height))
).

# Both must be true
We say that $x and $y overlap when (
  ($x xintersects $y)
  And
  ($x yintersects $y)
).

# Define the negation to simplify the grammar
We say that $x and $y don't overlap when (
  Not ($x and $y overlap)
).

"""
  @name Overlapping squares
  @description Coloured squares should not overlap in the page
  @severity Error
"""
For each $x in $(.liflab) (
  For each $y in $(.liflab) (
   ( $x's id equals $y's id )
   # This condition is necessary, as x and y may be the same square
  Or
   ( $x and $y don't overlap )
  )
).
